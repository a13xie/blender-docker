# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=embree
pkgver=4.3.2
pkgrel=1
epoch=20
pkgdesc="Collection of high-performance ray tracing kernels"
arch=('x86_64' 'aarch64')
url="https://embree.github.io/"
license=('Apache')
depends=('gcc-libs' 'glibc' 'intel-tbb')
makedepends=('cmake' 'ispc' 'freeglut' 'libxmu' 'ninja')
source=(git+https://github.com/$pkgname/$pkgname#tag=v$pkgver)
sha256sums=('4137eb62ffde3d59e7d15883fd0f720048171f973148226a4d8506c0229ae80b')

build() {
    # Embree detects actual ISA at runtime but we have to set a high maximum
    # buildtime version as it would otherwise default to the builder processor.
    cmake \
      -B build \
      -G Ninja \
      -S ${pkgname} \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DEMBREE_ISPC_SUPPORT=ON \
      -DEMBREE_TUTORIALS=OFF \
      -DEMBREE_BACKFACE_CULLING=OFF
    ninja -C build
    # Maybe enable these later once they are out of beta:
      # -DEMBREE_SYCL_SUPPORT=ON \
      # -DEMBREE_SYCL_LARGEGRF=ON
}

package() {
    DESTDIR="${pkgdir}" ninja -C build install

    mkdir "${pkgdir}"/usr/bin/
    mv "${pkgdir}"/usr/embree-vars.* "${pkgdir}"/usr/bin/
}
