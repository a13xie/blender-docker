# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=ispc
pkgver=1.18.1.r290+ge88e49e
pkgrel=1
pkgdesc="Compiler for high-performance SIMD programming on the CPU"
arch=('x86_64' 'aarch64')
url="https://ispc.github.io/"
license=(BSD)
depends=(ncurses zlib llvm-libs clang spirv-llvm-translator)
makedepends=(llvm python cmake level-zero-headers level-zero-loader openmp vc-intrinsics)
makedepends_x86_64=(lib32-glibc)
optdepends=(
    'intel-compute-runtime: GPU support'
    'level-zero-loader: GPU support'
    'openmp: GPU support'
)
_gtestcommit=bf0701daa9f5b30e5882e2f8f9a5280bcba87e77
_ispccommit=e88e49eb161aa5e75aecc843d479d5e757f5c43e
source=(ispc-${_ispccommit}.tar.gz::https://github.com/ispc/ispc/archive/${_ispccommit}.tar.gz
        googletest-${_gtestcommit}.tar.gz::https://github.com/google/googletest/archive/${_gtestcommit}.tar.gz
        ispc-libclang-linking.patch::https://github.com/ispc/ispc/pull/2422.patch)
sha256sums=('e0181f0f4dc78809166a8c28a46a3cb1f24d3dbbead4f4ff7e4ebfd8d109ebbd'
            'b44b5d37b62e7e54887f07f9862e7dce537aa9922e80995007bcffe6ab9058d4'
            'fdd8d27f1e7e55d414a1cd99b60758b3ef7aa6de729323213628108f4025274d')

prepare() {
  mv ispc-${_ispccommit} ispc-${pkgver} 
  cd ${pkgname}-${pkgver}
  # Build with unified libclang-cpp.so
  patch -p1 < ../ispc-libclang-linking.patch
  # Add Google Test
  rmdir ispcrt/tests/vendor/google/googletest
  mv ../googletest-${_gtestcommit} ispcrt/tests/vendor/google/googletest
}

build() {
  # Remove -Wp,-D_GLIBCXX_ASSERTIONS from CXXFLAGS as it leads to failures (https://github.com/ispc/ispc/issues/2283)
  export CXXFLAGS="$CFLAGS"
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DISPC_INCLUDE_EXAMPLES=OFF \
    -DXE_ENABLED=ON \
    -DISPC_INCLUDE_XE_EXAMPLES=OFF \
    -DXE_DEPS_DIR=/usr
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-${pkgver}/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
  # Remove uneeded files
  rm -r "${pkgdir}/home"
  rm "${pkgdir}"/usr/lib/libispcrt_static.a
}
