# Based on https://github.com/archlinux/svntogit-community/blob/packages/blender/trunk/PKGBUILD
#  - remove makedepend, optdepend on cuda
#  - remove depend on openimagedenoise
#  - Removed the unnecessary Optix source
#  - Disabled CUDA and Optix Cycles device support

pkgname=blender
pkgver=3.4.1
pkgrel=1
epoch=20
pkgdesc="A fully integrated 3D graphics creation suite"
arch=('x86_64' 'aarch64')
license=('GPL')
url="http://www.blender.org"
depends=('libpng' 'libtiff' 'openexr' 'python' 'desktop-file-utils' 'python-requests'
         'shared-mime-info' 'hicolor-icon-theme' 'xdg-utils' 'openjpeg2' 'glew' 'python-numpy'
         'freetype2' 'openal' 'ffmpeg' 'fftw' 'boost-libs' 'opencollada' 'openxr'
         'openimageio' 'libsndfile' 'jack' 'opencolorio' 'openshadinglanguage' 'alembic'
         'jemalloc' 'libspnav' 'ptex' 'opensubdiv' 'openvdb' 'log4cplus' 'sdl2' 'draco' 'libepoxy' 'embree')
depends_x86_64=('openimagedenoise')
makedepends=('cmake' 'boost' 'mesa' 'git' 'llvm' 'ninja' 'patch')
options=(!strip)
source=("git+https://projects.blender.org/blender/blender.git#tag=v$pkgver"
        "git+https://projects.blender.org/blender/blender-addons.git"
        "git+https://projects.blender.org/blender/blender-addons-contrib.git"
        "git+https://projects.blender.org/blender/blender-translations.git"
        "git+https://projects.blender.org/blender/blender-dev-tools.git"
        "git+https://github.com/DLTcollab/sse2neon.git#tag=v1.6.0"
        blender-numpy.patch
        force-draco1.patch
        force-draco2.patch
        blender-oneapi-escape.patch
        blender-sycl-path.patch
        blender-ffmpeg6.patch)
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'a39d9a13515b71d3b686d8f011b3eb9ee45de04f716627d711c5119317cb130c587da1b806f276f97064f814cfce3790b201731f047e5ec01e7d6f65acd04b3b'
            'e239da4f3906f1b54265435181cf770bae3d269c8d915df9a73861e6ee71ec70bf2339426e7c81a91e5a567273b3b3742d7a99feefd3398d821b26e1ff3a56d0'
            'c162646eba42ea7112cfb615466c2cbaddac8d254d4a9e91cf887c5bffef1325a983701163223a1fdee091310ac5574a400347aab7f52321825b5fa3d5a9036f'
            '1898af42ac10e9fc61c6fb979c1a3c6c91981c0c9790bf954e9425b5e7db897399d0ba6757f006f4ee1d6125ae0619b2e2a7ea5cf77cc36b91f3192873b66d9f'
            'bc9a80941a18aaddc3e8f2d9963e4f6b7eb1cf9f098418adb236771c3bedcab7b7a06121178f7498caba1af67116cd0ea549382e14a707163cdd5d8fbf3ee824'
            '34a2e2a344e851858fc8408ca9fd31d1d4a40083b2a1b2c01cabf48c2728c0b97226cf22c4224f93eda383b4690ff57e98291ad4fa81b960f0531067b40f78f7')

prepare() {
  cd "$pkgname"

  git submodule init
  git config submodule."release/scripts/addons".url "${srcdir}/blender-addons"
  git config submodule."release/scripts/addons_contrib".url "${srcdir}/blender-addons-contrib"
  git config submodule."release/datafiles/locale".url "${srcdir}/blender-translations"
  git config submodule."source/tools".url "${srcdir}/blender-dev-tools"
  git -c protocol.file.allow=always submodule update
  git submodule foreach git checkout v${pkgver}

  # fix build with boost 1.81.0
  git format-patch -1 --stdout 79837c5ed4b5 | patch -Np1

  # fix draco
  patch -p1 -i "$srcdir"/force-draco1.patch
  patch -p1 -d release/scripts/addons -i "$srcdir"/force-draco2.patch

  # fix for FS#77376
  patch -p1 -d release/scripts/addons -i "$srcdir"/blender-numpy.patch

  # fix SYCL include dir
  patch -p1 -i "$srcdir"/blender-sycl-path.patch

  # fix oneapi escapes
  patch -p1 -i "$srcdir"/blender-oneapi-escape.patch

  # ffmpeg 6
  patch -p1 -i "$srcdir"/blender-ffmpeg6.patch

}

get_pyver() {
  python -c 'import sys; print(str(sys.version_info[0]) + "." + str(sys.version_info[1]))'
}

build() {
  cd "$pkgname"

  local PYTHON_VER="$(get_pyver)"

  cmake . \
    -Bbuild \
    -GNinja \
    -Cbuild_files/cmake/config/blender_release.cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_INSTALL_PORTABLE=OFF \
    -DWITH_PYTHON_INSTALL=OFF \
    -DPYTHON_VERSION=$PYTHON_VER \
    -DPYTHON_LIBPATH=/usr/lib \
    -DPYTHON_LIBRARY=python$PYTHON_VER \
    -DPYTHON_INCLUDE_DIRS=/usr/include/python$PYTHON_VER \
    -DCMAKE_CXX_FLAGS="-I /usr/include/python$PYTHON_VER" \
    -DSSE2NEON_INCLUDE_DIR="$srcdir/sse2neon" \
    -DWITH_CYCLES_DEVICE_CUDA=OFF \
    -DWITH_CYCLES_DEVICE_OPTIX=OFF
  cmake --build build
}

package() {
  cd "$pkgname"

  DESTDIR="${pkgdir}" cmake --install build
  install -Dm755 release/bin/blender-softwaregl "${pkgdir}/usr/bin/blender-softwaregl"
  python -m compileall "${pkgdir}/usr/share/blender"
  python -O -m compileall "${pkgdir}/usr/share/blender"

  install -Dm644 release/freedesktop/org.blender.Blender.appdata.xml "${pkgdir}/usr/share/metainfo/org.blender.Blender.appdata.xml"

  # Manually install additional scripts (See FS#69351)
  cp -r release/scripts/addons_contrib/* "${pkgdir}"/usr/share/blender/3*/scripts/addons_contrib/

  # Manually install draco bindings (See FS#73415)
  mkdir -p "${pkgdir}/usr/lib/python$(get_pyver)"/
  mv "${pkgdir}"/usr/share/blender/3*/python/lib/* "${pkgdir}"/usr/lib/
  rm -r "${pkgdir}"/usr/share/blender/3*/python
}

